plugins {
    id 'groovy'
    id 'java'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.12.0'
    id 'org.ysb33r.gradletest' version '2.0'
}

group 'io.jumpco.open'
version '1.4.0'

apply from: 'gradle/publish.gradle'

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

pluginBundle {
    website = 'https://open.jumpco.io/projects/kfsm-viz.html'
    vcsUrl = 'https://github.com/open-jumpco/kfsm-viz-plugin'
    plugins {
        kfsmVizPlugin {
            displayName = 'KFSM Visualization plugin'
            description = 'KFSM Visualization plugin for plantuml and asciidoc'
            tags = ['fsm', 'kotlin', 'plantuml', 'asciidoc', 'visualize']
            version = project.version
        }
    }
}

gradlePlugin {
    plugins {
        kfsmVizPlugin {
            id = 'io.jumpco.open.kfsm.viz-plugin'
            implementationClass = 'io.jumpco.open.kfsm.gradle.VizPlugin'
        }
    }
}

gradleTest {
    versions '6.8.2', '6.7.1', '6.6.1', '6.5.1'
    versions '5.4.1', '5.5.1', '5.6.2'
    versions '5.0', '5.1.1', '5.2.1', '5.3.1'
    versions '4.9', '4.10.2'

    beforeTest {
        println "  ${it.name}"
    }
}

dependencies {
    compileOnly gradleApi()
    compileOnly localGroovy()
    implementation 'io.jumpco.open:kfsm-viz:1.0.6'
    testCompileOnly localGroovy()
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.assertj:assertj-core:3.6.2'
    testCompileOnly gradleTestKit()
    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.3-groovy-2.5'
}

